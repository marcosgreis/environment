""""""""""""""""""""
" Copied from https://vim.fandom.com/wiki/Filter_buffer_on_a_search_result
" Gather search hits, and display in a new scratch buffer.
function! Gather(pattern)
  if !empty(a:pattern)
    let save_cursor = getpos(".")
    let orig_ft = &ft
    " append search hits to results list
    let results = []
    execute "g/" . a:pattern . "/call add(results, getline('.'))"
    call setpos('.', save_cursor)
    if !empty(results)
      " put list in new scratch buffer
      new
      setlocal buftype=nofile bufhidden=hide noswapfile
      execute "setlocal filetype=".orig_ft
      call append(1, results)
      1d  " delete initial blank line
    endif
  endif
endfunction

" Delete the current buffer if it is a scratch buffer (any changes are lost).
function! CloseScratch()
  if &buftype == "nofile" && &bufhidden == "hide" && !&swapfile
    " this is a scratch buffer
    bdelete
    return 1
  endif
  return 0
endfunction
" End of copy
""""""""""""""""""""

function! FilterByRegister(register)
    let searchStr = getreg(a:register)
    call Gather(searchStr)
endfunction

vmap ;ff "gy :call FilterByRegister("g")<CR>
" nnoremap <Esc> :call CloseScratch()<CR>
